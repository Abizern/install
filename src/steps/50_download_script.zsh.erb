# Download zimfw script
local -r zscript=${ZIM_HOME}/zimfw.zsh
if (
  command mkdir -p ${ZIM_HOME} || return 1
  local -r zurl=https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
  if (( ${+commands[wget]} )); then
    command wget -nv -O ${zscript} ${zurl} || return 1
  elif (( ${+commands[curl]} )); then
    command curl -fsSL -o ${zscript} ${zurl} || return 1
  else
    print -u2 -P '%F{red}<%= error %>Either %Bwget%b or %Bcurl%b are required to download the Zim script.%f'
    return 1
  fi
); then
  print -PR "<%= okay %>Downloaded the Zim script to %B${zscript}%b"
else
  command rm -rf ${ZIM_HOME}
  print -u2 -PR "%F{red}<%= error %>Could not download the Zim script to %B${zscript}%b%f"
  return 1
fi
